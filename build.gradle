buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}


plugins {
    id 'maven'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'net.linguica.maven-settings'
apply plugin: 'com.jfrog.bintray'

group 'com.anigenero.junit'
version '1.0.2'

sourceCompatibility = 1.10

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    junitVersion = '5.2.+'
}

dependencies {

    implementation group: 'org.mockito', name: 'mockito-core', version: '2.18.+'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'junit-mockito-extension'
        userOrg = System.getenv('BINTRAY_USERNAME')
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anigenero/junit-mockito-extension.git'
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

def pomConfig = {

    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "anigenero"
            name "Robin Schultz"
            email "robin@anigenero.com"
        }
    }

    scm {
        url "https://github.com/anigenero/junit-mockito-extension"
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Extension for Mockito testing within JUnit 5')
                root.appendNode('name', 'junit-mockito-extension')
                root.children().last() + pomConfig
            }
        }
    }
}
